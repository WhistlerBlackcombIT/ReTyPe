/**
 * File containing Refill class to facilitate deleting multiple inventory locations from a NEW or CLONE inventory pool
 * Class will add itself to the parent retype instance
 *
 * AutoHotKey v1.1.13.01+
 *
 * LICENSE: This work is licensed under a version 4.0 of the
 * Creative Commons Attribution-ShareAlike 4.0 International License
 * that is available through the world-wide-web at the following URI:
 * http://creativecommons.org/licenses/by-sa/4.0/deed.en_US
 *
 * @category	Automation
 * @package		ReTyPe
 * @author		Dominic Wrapson <dwrapson@whistlerblackcomb.com>
 * @copyright	2015 Dominic Wrapson
 * @license		Creative Commons Attribution-ShareAlike 4.0 International License http://creativecommons.org/licenses/by-sa/4.0/deed.en_US
 */


; Trigger my damn self (in a horrible way due to AHK limitations)
objRetype.refill( new FluidInventoryLocationDelete() )


/**
 * Refill to automatically DELETE the following X inventory entries on NEW or CLONE inventory pools
 *
 * @category	Automation
 * @package		ReTyPe
 * @author		Dominic Wrapson <dwrapson@whistlerblackcomb.com>
 * @copyright	2015 Dominic Wrapson
 */
class FluidInventoryLocationDelete extends Fluid {

	strHotkey		:= ">^>!x"
	strMenuPath		:= "/Admin/Inventory"
	strMenuText		:= "Inventory Delete"
	intMenuIcon		:= 132 ;272

	/**
	 * Setup controls, window group, etc
	 */
	__New() {
		global objRetype
		base.__New()

		strGroup 	:= this.id
		strRTP		:= % objRetype.objRTP.classNN()
		GroupAdd, %strGroup%, Add Inventory Pool ahk_class %strRTP%
		GroupAdd, %strGroup%, Update ahk_class %strRTP%
	}

	/**
	 * Where the magic happens
	 */
	pour() {
		global objRetype
		static intDelete := 1

		; Activate RTP (after toolbar has been clicked)
		objRetype.objRTP.Activate()
; @todo Check needs to be removed once I've managed to wall-in shortcuts in to the RTP only window
		; Run if it's ready!
		strGroup := this.__Class
		IfWinActive, ahk_group %strGroup%
		{
			; Did you execute from an RTP window?
			if ( objRetype.objRTP.isActive() ) {
				idWinRTP	:= WinActive("A")

				; Are we in the pricing tab of a product header?
				if ( Window.CheckVisibleTextContains( "inventory pool location", "available inventory pool locations" ) ) {

					if ( Window.CheckControlFocus( objRetype.objRTP.formatClassNN( "SysListView32", this.getConf( "ListInventory", 11 ) ), "Inventory Location ListView" ) ) {
						; Prompt for iteration count
						intDelete := InputBox.show( "How many inventory entries do you wish to delete?", intDelete )

						Loop %intDelete%
						{
							WinActivate, Add Inventory Pool
							SendInput {Space}{AppsKey}d{Space}
						}
					}
				}
			}
		}
	}

}